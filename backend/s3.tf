# https://registry.terraform.io/providers/hashicorp/aws/latest/docs/resources/s3_bucket
#--------------------------------------------------------------------------------------
resource "aws_s3_bucket" "terraform_state" {
  bucket = var.s3_bucket_name
  tags = {
    "Name"        = var.s3_bucket_name
    "Description" = "S3 Remote Terraform State Store"
  }
  # lifecycle {
  #     prevent_destroy = true
  #    }
}

resource "aws_s3_bucket_versioning" "terraform_state" {
  bucket = aws_s3_bucket.terraform_state.id

  versioning_configuration {
    status = "Enabled"
  }
}

resource "aws_s3_bucket_server_side_encryption_configuration" "terraform_state" {
  bucket = aws_s3_bucket.terraform_state.id
  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "AES256"
    }
  }
}

resource "aws_s3_bucket_acl" "terraform_state" {
  bucket = aws_s3_bucket.terraform_state.id
  acl    = "private"
}

resource "aws_s3_bucket_public_access_block" "s3_access_block" {
  bucket                  = aws_s3_bucket.terraform_state.id
  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}
